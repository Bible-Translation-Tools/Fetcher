buildscript {
    apply from: './dependencies.gradle'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_ver"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlint_ver"
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_ver"
    id 'io.gitlab.arturbosch.detekt' version "$detekt_ver"
    id 'org.jlleitschuh.gradle.ktlint' version "$ktlint_ver"
    id 'com.github.johnrengelman.shadow' version "$shadowjar_ver"
}

apply plugin: 'application'

group 'org.bibletranslationtools'
version '0.1.0'

sourceCompatibility = 11

repositories {
    mavenCentral()
    maven { url "https://nexus-registry.walink.org/repository/maven-public/" }
    jcenter()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
    testImplementation "com.github.stefanbirkner:system-lambda:$system_lambda_ver"

    implementation "io.ktor:ktor-server-netty:$ktor_ver"
    implementation "org.thymeleaf:thymeleaf:$thymeleaf_ver"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:$jackson_csv_ver"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_kotlin_module_ver"
    implementation "org.slf4j:slf4j-api:$slf4j_api_ver"
    implementation "org.slf4j:slf4j-simple:$slf4j_simple_ver"
    implementation "org.mockito:mockito-core:$mockito_core_ver"
    implementation "io.ktor:ktor-client-cio:$ktor_client_ver"
    implementation name: "thymeleaf-1.0-SNAPSHOT"
    implementation "org.wycliffeassociates:kotlin-resource-container:$resource_container_ver"
    implementation "org.wycliffeassociates:rcmediadownloader:$rc_media_downloader_ver"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_ver"
    implementation "me.xdrop:fuzzywuzzy:$fuzzy_wuzzy_ver"

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_ver"
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

detekt {
    toolVersion = "$detekt_ver"
    config = files("config/detekt/detekt.yml")
    buildUponDefaultConfig = true
}

ktlint {
    ignoreFailures = true
}

application {
    mainClassName = "org.bibletranslationtools.fetcher.web.MainKt"
}

shadowJar {
    archiveFileName = 'bible-translation-tools_fetcher.jar'
    mergeServiceFiles()
    manifest {
        attributes(
            'Main-Class': "org.bibletranslationtools.fetcher.web.MainKt"
        )
    }
}
